"""
    eom_rossler_lorenz() -> Function

Equations of motions for a 6D unidirectionally coupled Rössler-Lorenz system.
The 3D Rössler attractor is coupled to  a 3D Lorenz attractor such that the
second component  (``x_2``) of the Rössler system unidirectionally forces
the second component (``y_2``) of the Lorenz system. This example is from [1].

The implementation here allows for tuning the parameters of each subsystem by
introducing the constants `a₁`, `a₂`, `a₃`, `b₁`, `b₂`, `b₃`. The default
values of these constants are as defined in Krakovská et al. (2018).

The dynamics is generated by the vector field:

```math
\\begin{aligned}
\\dot x_1 &= a_1(x_2 + x_3) \\
\\dot x_2 &= a_2(x_1 + 0.2x_2) \\
\\dot x_3 &= a_2(0.2 + x_3(x_1 - a_3)) \\
\\dot y_1 &= b_1(y_2 - y_1) \\
\\dot y_2 &= y_1(b_2 - y_3) - y_2 +c(x_2)^2 \\
\\dot y_3 &= y_1y_2 - b_3y_3
\\end{aligned}
```

with the coupling constant ``c \\geq 0``.

# References
[Krakovská, Anna, et al. "Comparison of six methods for the detection of causality in a bivariate time series." Physical Review E 97.4 (2018):042207](https://journals.aps.org/pre/abstract/10.1103/PhysRevE.97.042207)
"""
function eom_rossler_lorenz(u, p, t)
    a₁, a₂, a₃, b₁, b₂, b₃, c = (p...,)
    x₁, x₂, x₃, y₁, y₂, y₃ = (u...,)

    dx₁ = a₁*(x₂ + x₃)
    dx₂ = a₂*(x₁ + 0.2*x₂)
    dx₃ = a₂*(0.2 + x₃*(x₁ - a₃))
    dy₁ = b₁*(y₂ - y₁)
    dy₂ = y₁*(b₂ - y₃) - y₂ + c*x₂^2
    dy₃ = y₁*y₂ - b₃*y₃
    return SVector{6}(dx₁, dx₂, dx₃, dy₁, dy₂, dy₃)
end

function rossler_lorenz(u₀, a₁, a₂, a₃, b₁, b₂, b₃, c)
    p = [a₁, a₂, a₃, b₁, b₂, b₃, c]
    ContinuousDynamicalSystem(eom_rossler_lorenz, u₀, p)
end

"""
    rossler_lorenz(;u₀ = rand(6), a₁ = -6, a₂ = 6, a₃ = 2.0,
        b₁ = 10, b₂ = 28, b₃ = 8/3, c = 2) -> ContinuousDynamicalSystem

Initialise a Rössler-Lorenz system consisting of two independent 3D subsystems:
one Rössler attractor and one Lorenz attractor. They are coupled such that the
second component (`x₂`) of the Rössler system unidirectionally forces the
second component (`y₂`) of the Lorenz system.

The implementation here allows for tuning the parameters of each subsystem by
introducing the constants `a₁`, `a₂`, `a₃`, `b₁`, `b₂`, `b₃`. The default
values are as in Krakovská et al. (2018).

The dynamics is generated by the vector field:

```math
\\begin{aligned}
\\dot x_1 &= a_1(x_2 + x_3) \\
\\dot x_2 &= a_2(x_1 + 0.2x_2) \\
\\dot x_3 &= a_2(0.2 + x_3(x_1 - a_3)) \\
\\dot y_1 &= b_1(y_2 - y_1) \\
\\dot y_2 &= y_1(b_2 - y_3) - y_2 +c(x_2)^2 \\
\\dot y_3 &= y_1 y_2 - b_3y_3
\\end{aligned}
```

with the coupling constant ``c \\geq 0``.

# References
[Krakovská, Anna, et al. "Comparison of six methods for the detection of causality in a bivariate time series." Physical Review E 97.4 (2018):042207](https://journals.aps.org/pre/abstract/10.1103/PhysRevE.97.042207)
"""
rossler_lorenz(;u₀ = rand(6), a₁ = -6, a₂ = 6, a₃ = 2.0, b₁ = 10, b₂ = 28, b₃ = 8/3, c = 2) =
    rossler_lorenz(u₀, a₁, a₂, a₃, b₁, b₂, b₃, c)
