using LabelledArrays

export chuacircuit_nscroll_sine

@inline @inbounds function eom_chuacircuit_nscroll_sine(u, p, t)
    α, β, γ, a, b, c, σx, σy, σz = (p...,)
    x, y, z = (u...,)

    n::Int = c + 1
    if x >= 2*a*c
        fx = (b*pi/2*a)*(x - 2*a*c)
    elseif -2*a*c < x < 2*a*c
        d = ifelse(isodd(n), pi, 0)
        fx = -b*sin((pi*x/2*a) + d)
    elseif x <= -2*a*c
        fx = (b*pi/2*a)*(x + 2*a*c)
    end

    ηx = σx == 0 ? 0 : rand(Normal(0, σx))
    ηy = σy == 0 ? 0 : rand(Normal(0, σy))
    ηz = σz == 0 ? 0 : rand(Normal(0, σz))

    dx = α*(y - fx) + ηx
    dy = x - y + z + ηy
    dz = -β*y - γ*z + ηz
    return SVector{3}(dx, dy, dz)
end

function chuacircuit_nscroll_sine(u₀, α, β, γ, a, b, c::Int, σx, σy, σz)
    p = @LArray [α, β, γ, a, b, c, σx, σy, σz] (:α, :β, :γ, :a, :b, :c, :σx, :σy, :σz)
    ContinuousDynamicalSystem(eom_chuacircuit_nscroll_sine, u₀, p)
end

"""
    chuacircuit_nscroll_sine(;u₀ = [0.0, 0.0, 0.28695],
        α = 10.814, β = 14, γ = 0, a = 1.3, b = 0.11, c = 2,
        σx = 0.0, σy = 0.0, σz = 0.0)

Initialise an adjusted Chua system giving rise to n-scroll attractors [1].

## Equations of motion 

The dynamics is generated by the following vector field

```math
\\begin{aligned}
\\dot{x} = \\alpha (y - fx) + \\eta x \\\\
\\dot{y} = x - y + z + \\eta y \\\\
\\dot{z} = -\\beta y - \\gamma z + \\eta z 
\\end{aligned}
```

where ``\\eta x``, ``\\eta z``, and ``\\eta z`` are drawn independently from 
normal distributions with zero mean and standard deviations `σx`, `σy` 
and `σz` at each iteration.

``fx`` is given by the following conditions: 

```julia
n::Int = c + 1

if x >= 2*a*c
    fx = (b*pi/2*a)*(x - 2*a*c)
elseif -2*a*c < x < 2*a*c
    d = ifelse(isodd(n), pi, 0)
    fx = -b*sin((pi*x/2*a) + d)
elseif x <= -2*a*c
    fx = (b*pi/2*a)*(x + 2*a*c)
end
```

## References

1. Tang, Wallace KS, et al. "Generation of n-scroll attractors via 
    sine function." IEEE Transactions on Circuits and Systems I: 
    Fundamental Theory and Applications 48.11 (2001): 1369-1372.
"""
chuacircuit_nscroll_sine(;u₀ = [0.0, 0.0, 0.28695],
        α = 10.814, β = 14, γ = 0, a = 1.3, b = 0.11, c = 2,
        σx = 0.0, σy = 0.0, σz = 0.0) =
    chuacircuit_nscroll_sine(u₀, α, β, γ, a, b, c, σx, σy, σz)
